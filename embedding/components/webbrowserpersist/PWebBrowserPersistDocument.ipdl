/* -*- Mode: IDL; tab-width: 8; indent-tabs-mode: nil; c-basic-offset: 2 -*- */
/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

include protocol PBrowser;
include protocol PWebBrowserPersistDocumentRead;
include protocol PWebBrowserPersistDocumentWrite;

include InputStreamParams;

namespace mozilla {

// nsIWebBrowserPersistDocument has attributes which can be read
// synchronously.  To avoid using sync IPC for them, the actor sends
// this structure from the child to the parent before the parent actor
// is exposed to XPCOM.
struct WebBrowserPersistDocumentAttrs {
  bool isPrivate;
  nsCString documentURI;
  nsCString baseURI;
  nsCString contentType;
  nsCString characterSet;
  nsString title;
  nsString referrer;
  nsString contentDisposition;
  uint32_t cacheKey;
  uint32_t persistFlags;
  OptionalInputStreamParams postData;
  FileDescriptor[] postFiles;
};

// IPDL doesn't have tuples, so this gives the pair of strings from
// nsIWebBrowserPersistMap::getURIMapping a name.
struct WebBrowserPersistMapEntry {
  nsCString mapFrom;
  nsCString mapTo;
};

// nsIWebBrowserPersistMap is just copied over IPC as one of these,
// not proxied, to simplify the protocol.
struct WebBrowserPersistMap {
  WebBrowserPersistMapEntry[] mapURIs;
  nsCString targetBaseURI;
};

// This remotes nsIWebBrowserPersistDocument and its visitors.  The
// lifecycle is a little complicated: the initial document is
// constructed parent->child, but subdocuments are constructed
// child->parent and then passed back.  Subdocuments aren't subactors,
// because that would impose a lifetime relationship that doesn't
// exist in the XPIDL; instead they're all managed by the enclosing
// PBrowser (== TabParent/TabChild).
protocol PWebBrowserPersistDocument {
  manager PBrowser;
  manages PWebBrowserPersistDocumentRead;
  manages PWebBrowserPersistDocumentWrite;

parent:
  // The actor isn't exposed to XPCOM until after it gets one of these
  // two messages; see also the state transition rules.  The message
  // is either a response to the constructor (if it was parent->child)
  // or sent after it (if it was child->parent).
  Attributes(WebBrowserPersistDocumentAttrs aAttrs);
  InitFailure(nsresult aStatus);

child:
  SetPersistFlags(uint32_t aNewFlags);
  PWebBrowserPersistDocumentRead();
  PWebBrowserPersistDocumentWrite(WebBrowserPersistMap aMap,
                                  nsCString aRequestedContentType,
                                  uint32_t aEncoderFlags,
                                  uint32_t aWrapColumn);
  __delete__();

state START:
  recv Attributes goto MAIN;
  recv InitFailure goto FAILED;

state MAIN:
  send SetPersistFlags goto MAIN;
  send PWebBrowserPersistDocumentRead goto MAIN;
  send PWebBrowserPersistDocumentWrite goto MAIN;
  send __delete__;

state FAILED:
  send __delete__;
};

} // namespace mozilla
